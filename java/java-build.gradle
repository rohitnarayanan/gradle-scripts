buildscript {
	repositories {
		jcenter()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}

	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:2.2.1.RELEASE"
		classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
	}
}

/*
 * Other plugins need to be applied via the plugin class as this script is included in other scripts
 */
apply plugin: org.springframework.boot.gradle.plugin.SpringBootPlugin
apply plugin: io.spring.gradle.dependencymanagement.DependencyManagementPlugin


repositories {
	jcenter()
	mavenLocal()
	mavenCentral()
}

def buildProps = new Properties()
new URL("https://raw.githubusercontent.com/rohitnarayanan/gradle-scripts/master/java/java-build.properties").withInputStream { buildProps.load(it) }
project.ext.javaVersion = buildProps.getProperty("version.java")


/*
 * Java config
 */
compileJava {
	sourceCompatibility = javaVersion
	targetCompatibility = javaVersion
	
	options.compilerArgs += ["-Xlint:unchecked"]
}


/*
 * Test config
 */
test {
	useJUnitPlatform()
}

// enable jacoco reports
apply plugin: "jacoco"
test.finalizedBy jacocoTestReport


/*
 * Build config
 */
jar {
	manifest {
		attributes (
			"Build-Version": project.archiveVersion,
			"Build-Timestamp": new Date().format("yyyyMMdd-hhmmss.SSSZ"),
			"Build-Tool": "Gradle ${gradle.gradleVersion}",
			"Build-JDK" : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
			"Build-Platform" : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
			"Java-Source-Version": javaVersion,
			"Spring-Boot-Version": buildProps.getProperty("version.springboot")
		)
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = "javadoc"
	from javadoc.destinationDir
	javadoc.options.addBooleanOption("Xdoclint:none", true)
}

artifacts {
	archives sourcesJar
	archives javadocJar
}


/*
 * java-library config
 */
project.plugins.withId("java-library") {
	bootJar {
		enabled = false
	}
	
	jar {
		enabled = true
	}
}


/*
 * Eclipse config
 */
apply from: "https://raw.githubusercontent.com/rohitnarayanan/gradle-scripts/master/java/eclipse-config.gradle"
