buildscript {
	repositories {
		jcenter()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}

	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:2.2.1.RELEASE"
	}
}

/*
 * Other plugins need to be applied via the plugin class as this script is included in other scripts
 */
apply plugin: org.springframework.boot.gradle.plugin.SpringBootPlugin
apply plugin: io.spring.gradle.dependencymanagement.DependencyManagementPlugin


repositories {
	jcenter()
	mavenLocal()
	mavenCentral()
}

println "useLocalRepo="+project.ext.useLocalRepo
if(project.ext.useLocalRepo == "true") {
	project.ext.scriptURL = "http://localproxy.localhost.com:4080/devops/gradle-scripts/"
}
else {
	project.ext.scriptURL = "https://raw.githubusercontent.com/rohitnarayanan/gradle-scripts/master/"
}
println "scriptURL="+project.ext.scriptURL


def buildProps = new Properties()
new URL(project.ext.scriptURL + "java/java-build.properties").withInputStream {
	 stream -> buildProps.load(stream)
}
project.ext.javaVersion = buildProps.getProperty("version.java")

/*
 * Java config
 */
compileJava {
	sourceCompatibility = javaVersion
	targetCompatibility = javaVersion
	
	options.compilerArgs += ["-Xlint:unchecked"]
}


/*
 * Test config
 */
apply plugin: "jacoco"
test {
	useJUnitPlatform()
}

/*
 * Build config
 */
jar {
	manifest {
		attributes (
			"Build-Version": project.archiveVersion,
			"Build-Timestamp": new Date().format("yyyyMMdd-hhmmss.SSSZ"),
			"Build-Tool": "Gradle ${gradle.gradleVersion}",
			"Build-JDK" : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
			"Build-Platform" : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
			"Java-Source-Version": javaVersion,
			"Spring-Boot-Version": buildProps.getProperty("version.springboot")
		)
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = "javadoc"
	from javadoc.destinationDir
	javadoc.options.addBooleanOption("Xdoclint:none", true)
}

artifacts {
	archives sourcesJar
	archives javadocJar
}


/*
 * java-library config
 */
project.plugins.withId("java-library") {
	bootJar {
		enabled = false
	}
	
	jar {
		enabled = true
	}
}


/*
 * JCenter config
 */
apply from: project.ext.scriptURL + "java/jcenter-publish.gradle"


/*
 * Eclipse config
 */
apply from: project.ext.scriptURL + "java/eclipse-config.gradle"
